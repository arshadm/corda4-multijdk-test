apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'kotlin'

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }

    compileKotlin {
        kotlinOptions {
            allWarningsAsErrors = true
            apiVersion = 1.7
            languageVersion = 1.7
            jvmTarget = 1.8
        }
    }
}

configurations {
    jdk8
}

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation("net.corda:corda-node-driver:$corda_release_version") {
        exclude group: 'org.gradle', module: 'gradle-tooling-api'
    }
    testImplementation project(":cordapps:java-jdk8:helloworld-cordapp:workflows")

    testRuntimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2"
    testRuntimeOnly "org.jboss.byteman:byteman:4.0.11"

    // list all test dependencies so that gradle can resolve them and add them to the
    // test classpath, the application will then select the appropriate set to be
    // used by the nodes.
    jdk8 "co.paralleluniverse:quasar-core:$quasar_version"
    jdk8 "net.corda:corda:$corda_release_version"
    jdk8 "net.corda:corda-node:$corda_release_version"
    jdk8 "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2"
}

task copyJdk8Dependencies(type: Copy) {
    from configurations.jdk8
    into "${buildDir}/dependencies/jdk8"
}

tasks.named('test') {
    systemProperty 'dependencies_dir', "${buildDir}/dependencies"
}

test.dependsOn 'copyJdk8Dependencies'
