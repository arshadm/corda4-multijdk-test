apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'kotlin'

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }

    compileKotlin {
        kotlinOptions {
            allWarningsAsErrors = true
            apiVersion = 1.7
            languageVersion = 1.7
            jvmTarget = 1.8
        }
    }
}

configurations {
    jdk8
    jdk11
}

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation("net.corda:corda-node-driver:$corda_release_version") {
        exclude group: 'org.gradle', module: 'gradle-tooling-api'
    }
    testImplementation "net.corda:corda-finance-workflows-jdk11:$corda_release_version"
    testImplementation project(":cordapps:java-jdk8:helloworld-cordapp:workflows")
    testImplementation project(":cordapps:java-jdk11:helloworld-cordapp:workflows")

    testRuntimeOnly "org.jboss.byteman:byteman:4.0.11"

    // list all test dependencies so that gradle can resolve them and add them to the
    // test classpath, the application will then select the appropriate set to be
    // used by the nodes.
    jdk8 "co.paralleluniverse:quasar-core:$quasar_version"
    jdk8 "net.corda:corda:$corda_release_version"
    jdk8 "net.corda:corda-node:$corda_release_version"
    jdk8 project(":cordapps:java-jdk8:helloworld-cordapp:workflows")

    jdk11 "co.paralleluniverse:quasar-core:$quasar_jdk11_version"
    jdk11 "net.corda:corda-jdk11:$corda_release_version"
    jdk11 "net.corda:corda-node-jdk11:$corda_release_version"
    jdk11 "net.corda:corda-finance-contracts-jdk11:$corda_release_version"
    jdk11 "net.corda:corda-finance-workflows-jdk11:$corda_release_version"
    jdk11 project(":cordapps:java-jdk11:helloworld-cordapp:workflows")
}

task copyJdk8Dependencies(type: Copy) {
    from configurations.jdk8
    into "${buildDir}/dependencies/jdk8"
}

task copyJdk11Dependencies(type: Copy) {
    from configurations.jdk11
    into "${buildDir}/dependencies/jdk11"
}

tasks.named('test') {
    systemProperty 'dependencies_dir', "${buildDir}/dependencies"
    systemProperty 'jdk8_home', jdk8_home
    systemProperty 'jdk11_home', jdk11_home
}

test.dependsOn 'copyJdk8Dependencies'
test.dependsOn 'copyJdk11Dependencies'
